apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def releaseTime(){
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 22
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId 'com.gzfgeh.battlegame'
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        //dex 突破65535限制
        multiDexEnabled true
        //Androidmainifest.xml 里面UMENG_CHANNEL的Value为 ${UMENG_CHANNEL_VALUE}
        manifestPlaceholders = [UMENG_CHANNEL_VALUE:"umeng"]
    }

    //执行lint检查，遇到任何警告或者错误停止构建
    lintOptions{
        abortOnError true
    }

//    //签名
    signingConfigs{
        release{
            storeFile file("gzfgeh.jks")
            storePassword "gzfgeh"
            keyAlias "gzfgeh"
            keyPassword "gzfgeh"
        }
    }

    buildTypes {
        debug{
            buildConfigField "boolean","LOG_DEBUG","true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            //不显示log
            buildConfigField "boolean","LOG_DEBUG","false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            //移除无用resource
            shrinkResources true
            //加载默认混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "Gzfgeh_V${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    //渠道
    productFlavors {
        wandoujia {}
        baidu {}
        xiaomi {}
        tencent {}
        taobao {}
    }

    productFlavors.all{ flavor->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile files('libs/mina-core-2.0.9.jar')
    compile files('libs/slf4j-api-1.7.7.jar')
    compile files('libs/fastjson.jar')
}
